<!DOCTYPE html>
<html>
  <head>
    <title>shaders</title>
    <script src="node_modules/three/three.js"></script>
    <script src="node_modules/three/examples/js/controls/OrbitControls.js"></script>
    <script src="bower_components/threestrap/build/threestrap.min.js"></script>
    <script src="js/Lut.js"></script>
    <script src="js/rStats.js"></script>
    <script src="js/threestrap-rstats.js"></script>
    <style>
      canvas {
        background: radial-gradient(ellipse at center,  #7d7e7d 0%, #0e0e0e 100%);
      }
    </style>
  </head>
  <body>
    <div style="color: lightgray; background-color: black; position: absolute; top: 0px">
      Drag to rotate, mousewheel to zoom, rightclick-drag to pan.
    </div>
    <div style="color: lightgray; background-color: black; position: absolute; bottom: 0px">
      <a href="build/blackcube.html">blackcube</a>&nbsp;-&nbsp;
      <a href="build/sinecube.html">sinecube</a>&nbsp;-&nbsp;
      <a href="build/gradient.html">gradient</a>&nbsp;-&nbsp;
      <a href="build/beach.html">noisyball</a>
    </div>
    <script id="vertexShader" type="x-shader/x-vertex">
// high precision floats
#ifdef GL_ES
precision highp float;
#endif

attribute vec3 vertColor; // custom color for this vertex

uniform float time;

varying vec2 vUv;
varying vec3 vNormal; // the normal of the VERTEX
varying vec3 vCamera;


//--------------------------------
// https://github.com/ashima/webgl-noise
vec3 mod289(vec3 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec2 mod289(vec2 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec3 permute(vec3 x) {
  return mod289(((x*34.0)+1.0)*x);
}

float snoise(vec2 v) {
  const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                      0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                     -0.577350269189626,  // -1.0 + 2.0 * C.x
                      0.024390243902439); // 1.0 / 41.0
  // First corner
  vec2 i  = floor(v + dot(v, C.yy) );
  vec2 x0 = v -   i + dot(i, C.xx);

  // Other corners
  vec2 i1;
  i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
  vec4 x12 = x0.xyxy + C.xxzz;
  x12.xy -= i1;

  // Permutations
  i = mod289(i); // Avoid truncation effects in permutation
  vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
    + i.x + vec3(0.0, i1.x, 1.0 ));

  vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
  m = m*m;
  m = m*m;

  // Gradients: 41 points uniformly over a line, mapped onto a diamond.
  // The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

  vec3 x = 2.0 * fract(p * C.www) - 1.0;
  vec3 h = abs(x) - 0.5;
  vec3 ox = floor(x + 0.5);
  vec3 a0 = x - ox;

  // Normalise gradients implicitly by scaling m
  // Approximation of: m *= inversesqrt( a0*a0 + h*h );
  m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

  // Compute final noise value at P
  vec3 g;
  g.x  = a0.x  * x0.x  + h.x  * x0.y;
  g.yz = a0.yz * x12.xz + h.yz * x12.yw;
  return 130.0 * dot(m, g);
}
//--------------------------------

// Variables from THREE: https://github.com/mrdoob/three.js/blob/master/src/renderers/webgl/WebGLProgram.js#L153
void main()
{
  // passthroughs
  vNormal = normal;
  vUv = uv;
  vCamera = cameraPosition;
  vec3 newPosition = position;

  // get a continuously varying effect per vertex, and increment it through the noise space
  float noise = snoise(vec2(vNormal.x + time, vNormal.y + time));
  // scale the effect
  newPosition *= (1.0 + (noise / 8.0));

  gl_Position = projectionMatrix *
                modelViewMatrix *
                vec4(newPosition, 1.0);
}
    </script>
    <script id="fragmentShader" type="x-shader/x-fragment">
// high precision floats
#ifdef GL_ES
precision highp float;
#endif

varying vec3 vColor;
varying vec2 vUv;
varying vec3 vNormal; // the normal of the PIXEL
varying vec3 vCamera;

bool near(float var, float target)
{
  return (abs(target - var) < 0.0005) ? true : false;
}

void main()
{
  vec3 red = vec3(1.0, 0.3, 0.3);
  vec3 green = vec3(0.3, 1.0, 0.3);
  vec3 blue = vec3(0.3, 0.3, 1.0);
  vec3 brown = vec3(0.7, 0.4, 0.2);
  vec3 white = vec3(1.0, 1.0, 1.0);
  vec3 black = vec3(0.0, 0.0, 0.0);

  vec3 rgb;

  if (vUv.y > 0.95)
  {
    rgb = white;
  }
  else if (vUv.y < 0.05)
  {
    rgb = white;
  }
  else if (vUv.x < 0.17) { rgb = red; }
  else if (vUv.x < 0.33) { rgb = white; }
  else if (vUv.x < 0.49) { rgb = green; }
  else if (vUv.x < 0.65) { rgb = white; }
  else if (vUv.x < 0.81) { rgb = blue; }
  else { rgb = vec3(1.0, 1.0, 1.0); }

  if (near(vUv.y, 0.95) || near(vUv.y, 0.05)) { rgb = black; }
  else if (near(vUv.y, 0.36) || near(vUv.y, 0.64)) { rgb = black; }
  else if (near(vUv.x, 0.17) || near(vUv.x, 0.33) || near(vUv.x, 0.49)
            || near (vUv.x, 0.65) || near(vUv.x, 0.81) || near(vUv.x, 0.0)) { rgb = black; }

  vec3 cameraNormal = normalize(vCamera);
  float alignmentToCamera = dot(vNormal, cameraNormal);
  // lighting: range from facing the camera = 120% brightness, to facing away = 20%.
  rgb *= 0.2 + alignmentToCamera;

  // hacky specular highlight  
  float alignmentToSpotlight = dot(vNormal, normalize(vec3(1.0 ,1.0 ,1.0)));
  if (alignmentToSpotlight > 0.95) { rgb = mix(rgb, white, (alignmentToSpotlight / 3.0)); }
  
  gl_FragColor  = vec4(rgb, 1.0); // last value is alpha
}
    </script>
    <script src="js/main.js"></script>
  </body>
</html>
